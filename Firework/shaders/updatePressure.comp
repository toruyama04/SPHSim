#version 460

/* 
   Update Pressure: 8
    - involves producing a repellant/attractive force
*/

layout(local_size_x = 256) in;

layout(std430, binding = 10) buffer NeighbourListSSBO {
    uint neighbourList[];
};

layout(std430, binding = 11) buffer EndIndex {
    uint endIndices[];
};

layout(std430, binding = 2) buffer Positions {
    vec4 positions[];
};

layout(std430, binding = 3) buffer ForcesSSBO {
    vec4 forces[];
};

layout(std430, binding = 4) buffer DensitySSBO {
    float densities[];
};


uniform float restDensity;
uniform float h;
uniform float k1;
uniform float k2;
uniform uint boundaryParticleNum;
uniform uint fluidParticleNum;
uniform uint maxNeighbourNum;
uniform float sigma;
uniform float mass;


void main()
{
    uint idx = gl_GlobalInvocationID.x;
    uint totalidx = gl_GlobalInvocationID.x + boundaryParticleNum;
    if (idx >= fluidParticleNum) return;

    vec3 x_i = positions[totalidx].xyz;
    float d_i = densities[totalidx];

    // pressure computation
    float p_i = k1 * (pow((d_i / restDensity), k2) - 1.0);

    vec3 pressureVal = vec3(0.0);

    uint startIndex = idx * maxNeighbourNum;
    uint endIndex = endIndices[idx];
    for (uint i = 0; i < endIndex; ++i)
    {
        uint ni = neighbourList[startIndex + i];

        vec3 x_j = positions[ni].xyz;
        float d_j = densities[ni];

        // mirror pressure if boundary particle
        float p_j = (ni < boundaryParticleNum) ? p_i : k1 * (pow((d_j / restDensity), k2) - 1.0);
   
        vec3 r_ij = x_i - x_j;
        float r2 = dot(r_ij, r_ij);

        float r = sqrt(r2);
        vec3 dir = r_ij / r;

        // spiky kernel gradient
        float factor = sigma * (h - r) * (h - r);
        vec3 gradW = factor * dir;

        float coeff = mass * (p_i / (d_i * d_i) + p_j / (d_j * d_j));
        pressureVal -= coeff * gradW;
    }
    forces[idx].xyz = pressureVal;
}
