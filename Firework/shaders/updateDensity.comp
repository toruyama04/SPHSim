#version 460

layout(local_size_x = 256) in;

layout(std430, binding = 0) buffer Positions {
    vec4 positions[];
};

layout(std430, binding = 6) buffer DensitySSBO {
    float densities[];
};

layout(std430, binding = 14) buffer NeighbourListSSBO {
    uint neighbourList[];
};

layout(std430, binding = 16) buffer EndIndex {
    uint endIndex[];
};

uniform float h;
uniform float h3;
uniform float mass;
uniform uint particleNum;
uniform uint maxNeighbourNum;
uniform float sigma;


void main()
{
    uint idx = gl_GlobalInvocationID.x;
    if (idx >= particleNum) return;

    uint startIndex = idx * maxNeighbourNum;
    uint endIndex = endIndex[idx];

    float densitySum = 0.0;
    vec3 x_i = positions[idx].xyz;

    // going through each neighbour
    for (uint i = startIndex; i < endIndex; ++i)
    {
        uint ni = neighbourList[i];
        vec3 x_j = positions[ni].xyz;
        float r = abs(distance(x_i, x_j));
        
        float q = r / h;
        float w1 = 6.0 * (q * q * q - q * q) + 1.0;
        float w2 = 2.0 * pow(1.0 - q, 3.0);

        float pieceWise1 = step(0.0, q) * step(q, 0.5); 
        float pieceWise2 = step(0.5, q) * step(q, 1.0);
        float w = pieceWise1 * w1 + pieceWise2 * w2;
        w *= sigma;

        w *= step(q, 1.0001);
        densitySum += w * mass;
    }
    densities[idx] = densitySum;
}