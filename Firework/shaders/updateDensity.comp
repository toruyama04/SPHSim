#version 460

layout(local_size_x = 256) in;

layout(std430, binding = 2) buffer predPositionsSSBO {
    vec4 predPositions[];
};

layout(std430, binding = 0) buffer positionsSSBO {
    vec4 positions[];
};

layout(std430, binding = 3) buffer forcesSSBO {
    vec4 forces[];
};

layout(std430, binding = 4) buffer DensitySSBO {
    float densities[];
};

layout(std430, binding = 10) buffer NeighbourListSSBO {
    uint neighbourList[];
};

layout(std430, binding = 11) buffer EndIndex {
    uint endIndices[];
};

uniform float h;
uniform float h3;
uniform float mass;
uniform uint fluidParticleNum;
uniform uint boundaryParticleNum;
uniform uint maxNeighbourNum;
uniform float sigma;


void main()
{
    // skip the boundary particles, we index from the first fluid particle
    uint idx = gl_GlobalInvocationID.x;
    uint totalidx = gl_GlobalInvocationID.x + boundaryParticleNum;
    if (idx >= fluidParticleNum) return;

    // resetting forces as well
    forces[idx] = vec4(0.0);

    uint startIndex = idx * maxNeighbourNum;
    uint endIndex = endIndices[idx];

    float densitySum = 0.0;
    vec3 x_i = predPositions[totalidx].xyz;

    // going through each neighbour
    for (uint i = 0; i < endIndex; ++i)
    {
        uint ni = neighbourList[startIndex + i];
        vec3 x_j = predPositions[ni].xyz;

        float r = distance(x_i, x_j);
        
        float q = clamp(r / h, 0.0, 1.0);
        float w1 = 6.0 * (q * q * q - q * q) + 1.0;
        float w2 = 2.0 * pow(1.0 - q, 3.0);

        float pieceWise1 = step(0.0, q) * step(q, 0.5); 
        float pieceWise2 = step(0.5, q) * step(q, 1.0);
        float w = pieceWise1 * w1 + pieceWise2 * w2;
        w *= sigma;

        w *= step(q, 1.0001);
        densitySum += w * mass;
    }
    // write back to densities, buffer is only for fluid particles, so dont skip
    densities[totalidx] = densitySum;
}